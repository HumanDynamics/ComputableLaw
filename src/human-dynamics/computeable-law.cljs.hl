(page "index.html"
  (:refer-clojure
    :exclude [-])
  (:refer-hoplon
    :exclude [elem rt title input form b s section])
  (:require
    [clojure.string         :refer [capitalize]]
    [hoplon.ui              :refer [elem image video *data*]]
    [hoplon.ui.attrs        :refer [- c r s b d]]
    [hoplon.material        :refer [field action-button]]
    [hoplon.material.colors :as c :refer [white teal grey grey-300 black]])
  (:require-macros
    [hoplon.ui :refer [button form window]]))

;;; constants ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(def organization "Legal Technology Laboratory")

(def content-url  "http://data.computablelaw.org/content.edn")
(def resource-url "http://data.computablelaw.org/resources/")
(def icon-url     "https://fonts.googleapis.com/icon?family=Material+Icons")
(def font-url     "https://fonts.googleapis.com/css?family=RobotoDraft:400,500,700,400italic")

;;; utils ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn ->time [str]
  (.toLocaleTimeString (js/Date. str) (.-language js/navigator) #js{:hour "2-digit" :minute "2-digit"}))

;;; state ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defc db {})
(defc menu-open false)

;;; service ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(.get js/jQuery content-url #(swap! db assoc :views (cljs.reader/read-string %)))

;;; query ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defc= route      (->  db :route))
(defc= state      (->  route ffirst))
(defc= qargs      (->  route second))
(defc= title      (when state (capitalize (name state))))
(defc= view-title (str organization " " title))

(defc= views      (-> db :views))
(defc= events     (-> views :events :items))
(defc= event      (get events (:id qargs)))
(defc= photos     (-> views :photos :items))
(defc= photo      (get photos (:id qargs)))

;;; command ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn change-route [db route]
  (assoc db :route route))

(defn change-state [db path & [query]]
  (change-route db [path query]))

(defn initiate [db route status _]
  (change-route db (if (empty? route) [[:conference]] route)))

;;; view ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; styles ;;;

; breakpoints
(def sm 760)
(def md 1240)
(def lg 1480)

; sizes
(def pad-sm 8)
(def pad-md 16)
(def pad-lg 24)

;; colors

(def font-black (c 0x333333))

; fonts
(def helvetica ["HelveticaNeue-Bold" "Lucida Grande" :sans-serif])

; text
(defelem section-title [attrs elems]
  (elem :w (r 1 1) :f 42 :ff helvetica :fx :uppercase
    attrs elems))

(defelem title-text [attrs elems]
  (elem :w (r 1 1) :f 24 attrs elems))

(defelem subtitle-text [attrs elems]
  (elem :w (r 1 1) :pt 8 :fi :italic attrs elems))

(defelem body-text [attrs elems]
  (elem :w (r 1 1) attrs elems))

(defelem container [attrs elems]
  (elem :w (b :w (r 1 1) sm (r 7 10)) :h (r 1 1) :pt pad-sm :pb (* pad-sm 2) :g pad-sm attrs elems))

(defelem section [{c :color :as attrs} elems]
  (elem :w (r 1 1) :ah :center :c c
    (container
      (dissoc attrs :color) elems)))

;;; components ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defelem icon [attrs elems]
  (elem :f 24 :fh 24 :ff "Material Icons" :m :pointer :fr :optimizeLegibility
    attrs elems))

(defelem footer-menu [attrs elems]
  (elem :w (r 1 1) attrs
    (let [n (count elems)]
      (for-tpl [elem elems]
         (elem :w (r 1 n))))))

(defelem card [{:keys [title url] :as attrs} elems]
  (elem :bc grey-300 (dissoc attrs :title :url) :d (d 0 1 (c 0 0 0 0.37) 4)
    (image :url url)
    (elem :w (r 1 1) :p pad-md :g pad-sm
      (elem :w (r 1 1) :f 16 :fh 15 :fc font-black :ft :500
        title)
      (elem :w (r 1 1) :f 14 :fh 15 :fc font-black
        elems))))

(defelem feature-box [{:keys [title] :as attrs} elems]
  (elem :w (b :w (r 1 3) sm 100) (dissoc attrs :title)
    (elem :w (r 1 1) :ph 3 :av :middle
      title)
    (elem :w (r 1 1) :h (- (r 1 1) 26) :f 30
     elems)))

;;; components ;;;

;;; view  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn home-view []
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title view-title)))

(defelem event-view [attrs _]
  (section :h 800
    (title-text    (cell= (:title event)))
    (subtitle-text (cell= (apply str (interpose " " (:speakers event)))))
    (body-text     (cell= (:description event)))))

(defelem events-view [_ _]
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title :w (r 1 1) view-title)
    (for-tpl [[idx {:keys [title speakers description start-time stop-time]}] (cell= (map-indexed vector events))]
      (elem :gh 64
        (elem :w 76 :gv 16 :bl 2 :bc (c 0 0 0 0.25) :ah :right
          (elem (cell= (->time start-time)))
          (elem (cell= (->time stop-time))))
        (elem :w (- (r 1 1) 150) :m :pointer :click #(swap! db change-state [:event] {:id @idx})
          (elem :w (r 1 1) :f 23
             title)
          (elem :w (r 1 1) :pb 12 :fi :italic
             (cell= (apply str (interpose " " speakers))))
          (elem :w (r 1 1) :ft :100
             (cell= (when description (subs description 0 100)))))))))

(defn projects-view []
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title view-title)))

(defn organizers-view []
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title view-title)))

(defelem photo-view [attrs _]
  (elem :w (r 1 1) :h (r 1 1) :p 40 :ah :center :av :middle
    (title-text       (cell= (:title photo)))
    #_(subtitle-text    (cell= (:description photo)))
    (image :w (r 1 1) :d (d 0 1 (c 0 0 0 0.37) 4) :url (cell= (str resource-url (:image photo))) :click #(swap! db change-state [:photos]))))

(defn photos-view []
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title view-title)
    (for-tpl [[idx {:keys [thumb]}] (cell= (map-indexed vector photos))]
      (image :w (b :w (r 1 1) sm (r 1 2) md (r 1 3) lg (r 1 4)) :d (d 0 1 (c 0 0 0 0.37) 4) :m :pointer :click #(swap! db change-state [:photo] {:id @idx}) :url (cell= (str resource-url thumb))))))

(defn notes-view []
  (elem :w (r 1 1) :h (- (r 1 1) 64) :p 42 :g 42
    (section-title view-title)))

(window
  :title        (cell= (str organization " - " title))
  :route        route
  :styles       [icon-url font-url]
  :initiated    (partial swap! db initiate)
  :routechanged (partial swap! db change-route)
  :ff helvetica :fr :optimizeLegibility :fm :antialiased
  (when-tpl (b :w menu-open lg true)
    (elem :w 240 :h (r 1 1) :pv 40 :gv 30 :xl (b :w menu-open lg false) :ah :center :c black :click #(reset! menu-open false)
      (image :h 130 :m :pointer :url "mit-ml-logo.jpg" :click #(do (reset! menu-open false) (swap! db change-state [:conference])))
      (elem :w (r 1 1) :h (- (r 1 1) (+ 130 84)) :gv 46 :bc grey-300
        (for-tpl [[key {:keys [title]}] views]
          (elem :w (r 1 1) :ph 40 :pv 4 :av :middle :bl 3 :bcl (cell= (if (= key state) white black)) :f 18 :ff helvetica :fc white :m :pointer :click #(do (reset! menu-open false) (swap! db change-state [@key]))
            (elem :pt 2
              title))))
      (elem :w (r 1 1) :ph 36 :ff helvetica :fc white
        (image :w 130 :h 54 :url "creative-commons-logo.png" :m :pointer :click #(.open js/window "https://creativecommons.org/")))))
  (elem :w (b :w (r 1 1) lg (- (r 1 1) 240)) :h (r 1 1) :sv :auto
    (elem :w (r 1 1) :h 64 :ph pad-lg :pv pad-sm :g pad-sm :av :middle :bb 0.5 :bc (c 0 0 0 0.15)
      (icon :click #(swap! menu-open not) :fc black "menu")
      (elem :w (- (r 1 1) 24 pad-sm) :g pad-sm :ah :right :av :middle
        (image :w 130 :url "kauffman-logo.png" :m :pointer :click #(.open js/window "http://www.kauffman.org/"))))
    (case-tpl state
      :conference (home-view)
      :event      (event-view)
      :events     (events-view)
      :projects   (projects-view)
      :organizers (organizers-view)
      :photo      (photo-view)
      :photos     (photos-view)
      :notes      (notes-view)))
  (elem :xl 240 :xr 0 :w (r 1 1) :h (r 1 1) :c (c 0 0 0 0.5) :v (b :w menu-open lg false) :click #(reset! menu-open false)))
